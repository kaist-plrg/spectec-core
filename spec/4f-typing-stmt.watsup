;;
;; Empty statement
;;

rule Stmt_ok/emptys:
  p C f |- EmptyS : C f EmptyS

;;
;; Assignment statement
;;

rule Lval_ok/namee:
  p C |- NameE name _
  -- if typ dir _ _ = $find_styp(p, C, name)
  -- if dir = OUT \/ dir = INOUT
  -- if $is_assignable(typ)

rule Lval_ok/arracce:
  p C |- ArrAccE exprIL_b exprIL_i _
  -- Lval_ok: p C |- exprIL_b

rule Lval_ok/bitacce:
  p C |- BitAccE exprIL_b exprIL_l exprIL_h _
  -- Lval_ok: p C |- exprIL_b

rule Lval_ok/expracce:
  p C |- ExprAccE exprIL_b member _
  -- Lval_ok: p C |- exprIL_b

rule Stmt_ok/assigns:
  p C f |- AssignS expr_l expr_r : C f (AssignS exprIL_l exprIL_r')
  -- Expr_ok: p C |- expr_l : exprIL_l
  -- if `(typ_l; DYN) = $annot(exprIL_l)
  -- Lval_ok: p C |- exprIL_l
  ----
  -- Expr_ok: p C |- expr_r : exprIL_r
  -- if `(typ_r; _) = $annot(exprIL_r)
  ----
  -- if exprIL_r' = $coerce_assign(exprIL_r, typ_l)

;;
;; Switch statement
;;

relation Switch_tbl_label_ok:
  cursor context id bool |- switchlabel : switchlabelIL
  hint(input %0 %1 %2 %3 %4)

rule Switch_tbl_label_ok/exprl:
  p C id_t b_last |- ExprL (NameE (CURRENT id_a)) : ExprL (NameE (CURRENT id_a) `(typ_e; ctk_e))
  -- if id_e = $concat_text([ "action_list(", id_t, ")" ])
  -- if id = $concat_text([ id_e, ".", id_a ])
  -- if TableEnumFieldV id_e id_a = $find_val(p, C, CURRENT id)
  -- if typ_e _ ctk_e _ = $find_styp(p, C, CURRENT id)

rule Switch_tbl_label_ok/defaultl:
  p C id_t true |- DefaultL : DefaultL


relation Switch_tbl_case_ok:
  cursor context flow id bool |- switchcase : flow switchcaseIL switchlabel
  hint(input %0 %1 %2 %3 %4 %5)

rule Switch_tbl_case_ok/matchc:
  p C f id_t b_last |- MatchC switchlabel block : f' (MatchC switchlabelIL blockIL) switchlabel
  -- Switch_tbl_label_ok: p C id_t b_last |- switchlabel : switchlabelIL
  -- Block_ok: p C f NOINIT |- block : C' f' blockIL

rule Switch_tbl_case_ok/fallc:
  p C f id_t b_last |- FallC switchlabel : f (FallC switchlabelIL) switchlabel
  -- Switch_tbl_label_ok: p C id_t b_last |- switchlabel : switchlabelIL


relation Switch_tbl_cases_ok:
  cursor context flow id |- switchcase* : flow switchcaseIL* switchlabel*
  hint(input %0 %1 %2 %3 %4)

rule Switch_tbl_cases_ok/empty:
  p C f id_t |- eps : f eps eps

rule Switch_tbl_cases_ok/seq:
  p C f id_t |- switchcase_h :: switchcase_t* : f'' (switchcaseIL_h :: switchcaseIL_t*) (switchlabel_h :: switchlabel_t*)
  -- if b_last = (switchcase_t* = eps)
  -- Switch_tbl_case_ok: p C f id_t b_last |- switchcase_h : f' switchcaseIL_h switchlabel_h
  -- Switch_tbl_cases_ok: p C f' id_t |- switchcase_t* : f'' switchcaseIL_t* switchlabel_t*


rule Stmt_ok/switchs-tbl:
  LOCAL C f |- SwitchS expr switchcase* : C f' (SwitchS exprIL switchcaseIL*)
  -- if C.LOCAL.KIND = CONTROLAPPLYMETHOD
  -- Expr_ok: LOCAL C |- expr : exprIL
  -- if `(TableEnumT id _; _) = $annot(exprIL)
  -- if id_t = $strip_prefix($strip_suffix(id, ")"), "action_list(")
  -- Switch_tbl_cases_ok: LOCAL C f id_t |- switchcase* : f' switchcaseIL* switchlabel*
  -- if $distinct_<switchlabel>(switchlabel*)


relation Switch_gen_label_ok:
  cursor context typ bool |- switchlabel : switchlabelIL
  hint(input %0 %1 %2 %3 %4)

rule Switch_gen_label_ok/exprl:
  p C typ_s b_last |- ExprL expr : ExprL exprIL'
  -- Expr_ok: p C |- expr : exprIL
  -- if `(_; LCTK) = $annot(exprIL)
  -- if exprIL' = $coerce_assign(exprIL, typ_s)

rule Switch_gen_label_ok/defaultl:
  p C typ_s true |- DefaultL : DefaultL


relation Switch_gen_case_ok:
  cursor context flow typ bool |- switchcase : flow switchcaseIL switchlabel
  hint(input %0 %1 %2 %3 %4 %5)

rule Switch_gen_case_ok/matchc:
  p C f typ_s b_last |- MatchC switchlabel block : f' (MatchC switchlabelIL blockIL) switchlabel
  -- Switch_gen_label_ok: p C typ_s b_last |- switchlabel : switchlabelIL
  -- Block_ok: p C f NOINIT |- block : C' f' blockIL

rule Switch_gen_case_ok/fallc:
  p C f typ_s b_last |- FallC switchlabel : f (FallC switchlabelIL) switchlabel
  -- Switch_gen_label_ok: p C typ_s b_last |- switchlabel : switchlabelIL


relation Switch_gen_cases_ok:
  cursor context flow typ |- switchcase* : flow switchcaseIL* switchlabel*
  hint(input %0 %1 %2 %3 %4)

rule Switch_gen_cases_ok/empty:
  p C f typ_s |- eps : f eps eps

rule Switch_gen_cases_ok/seq:
  p C f typ_s |- switchcase_h :: switchcase_t* : f'' (switchcaseIL_h :: switchcaseIL_t*) (switchlabel_h :: switchlabel_t*)
  -- if b_last = (switchcase_t* = eps)
  -- Switch_gen_case_ok: p C f typ_s b_last |- switchcase_h : f' switchcaseIL_h switchlabel_h
  -- Switch_gen_cases_ok: p C f' typ_s |- switchcase_t* : f'' switchcaseIL_t* switchlabel_t*


rule Stmt_ok/switchs-gen:
  LOCAL C f |- SwitchS expr switchcase* : C f' (SwitchS exprIL switchcaseIL*)
  -- if C.LOCAL.KIND = CONTROLAPPLYMETHOD
  -- Expr_ok: LOCAL C |- expr : exprIL
  -- if `(typ; _) = $annot(exprIL)
  ----
  -- if typ' = $canon_typ(typ)
  -- if $is_errt(typ') \/ $is_fintt(typ') \/ $is_fbitt(typ') \/ $is_enumt(typ') \/ $is_senumt(typ')
  ----
  -- Switch_gen_cases_ok: LOCAL C f typ |- switchcase* : f' switchcaseIL* switchlabel*
  -- if $distinct_<switchlabel>(switchlabel*)

;;
;; If statement
;;

rule Stmt_ok/ifs:
  p C f |- IfS expr_c stmt_t stmt_f : C f' (IfS exprIL_c stmtIL_t stmtIL_f)
  -- Expr_ok: p C |- expr_c : exprIL_c
  -- if `(BoolT; _) = $annot(exprIL_c)
  -- Stmt_ok: p C f |- stmt_t : C_t f_t stmtIL_t
  -- Stmt_ok: p C f |- stmt_f : C_f f_f stmtIL_f
  -- if f' = $join_flow(f_t, f_f)

;;
;; Block statement
;;

rule Block_ok/init:
  LOCAL C f INIT |- BlockB stmt* : C' f' (BlockB stmtIL*)
  -- Stmts_ok: LOCAL C f |- stmt* : C' f' stmtIL*

rule Block_ok/noinit:
  LOCAL C f NOINIT |- BlockB stmt* : C''' f' (BlockB stmtIL*)
  -- if C' = $enter(C)
  -- Stmts_ok: LOCAL C' f |- stmt* : C'' f' stmtIL*
  -- if C''' = $exit(C'')

rule Stmt_ok/blocks:
  LOCAL C f |- BlockS block : C' f' (BlockS blockIL)
  -- Block_ok: LOCAL C f NOINIT |- block : C' f' blockIL

;;
;; Exit statement
;;

rule Stmt_ok/exits:
  p C f |- ExitS : C f ExitS

;;
;; Return statement
;;

rule Stmt_ok/rets-none:
  LOCAL C f |- RetS eps : C RET (RetS eps)
  -- if VoidT = $get_typ_ret_lkind(C.LOCAL.KIND)

rule Stmt_ok/rets-some:
  LOCAL C f |- RetS expr : C RET (RetS exprIL')
  -- Expr_ok: LOCAL C |- expr : exprIL
  -- if typ_r = $get_typ_ret_lkind(C.LOCAL.KIND)
  ----
  -- if exprIL' = $coerce_assign(exprIL, typ_r)

;;
;; Function call statement
;;

rule Stmt_ok/callfuncs:
  p C f |- CallFuncS name targ* arg* : C f (CallFuncS name targIL'* argIL'*)
  -- if (name = CURRENT "verify")
        => ((p = BLOCK /\ C.BLOCK.KIND = PARSER) \/ (p = LOCAL /\ C.LOCAL.KIND = PARSERSTATE))
  -- (Type_ok: p C |- targ : targIL tid_fresh_a*)*
  -- if tid_fresh* = $concat_<tid>(tid_fresh_a**)
  ----
  -- FuncType_ok: p C |- name targIL* arg* : ft tid_fresh_inserted* id_deft*
  ----
  -- if tid_fresh'* = $concat_<tid>(tid_fresh* ++ tid_fresh_inserted*)
  ----
  -- Call_ok: p C tid_fresh'* |- ft targIL* arg* id_deft* : typ targIL'* argIL'*

;;
;; Method call statement
;;

rule Stmt_ok/callmethods:
  p C f |- CallMethodS expr_b member targ* arg* : C f (CallMethodS exprIL_b member targIL'* argIL'*)
  -- (Arg_ok: p C |- arg : argIL typ_arg)*
  ----
  -- (Type_ok: p C |- targ : targIL tid_fresh_a*)*
  -- if tid_fresh* = $concat_<tid>(tid_fresh_a**)
  ----
  -- MethodType_ok: p C |- expr_b member targIL* arg* : ft exprIL_b tid_fresh_inserted* id_deft*
  ----
  -- if tid_fresh'* = $concat_<tid>(tid_fresh* ++ tid_fresh_inserted*)
  ----
  -- Call_ok: p C tid_fresh'* |- ft targIL* arg* id_deft* : typ targIL'* argIL'*

;;
;; Instantiation statement
;;

rule Stmt_ok/callinsts:
  p C f |- CallInstS name targ* arg* : C f (CallInstS typ name targIL'* argIL'*)
  -- ConsType_ok: p C |- name eps eps : ct eps eps
  ----
  -- Inst_ok: p C eps |- ct eps eps eps : typ eps eps
  ----
  -- if typ' = $canon_typ(typ)
  -- if $is_parsert(typ') \/ $is_controlt(typ')
  ----
  -- if id = $id_of_name(name)
  -- if C' = $add_styp(p, C, id, typ NO CTK eps)
  ----
  -- Stmt_ok: p C' f |- CallMethodS (NameE (CURRENT id)) "apply" targ* arg* : C'' f' stmtIL
  -- if stmtIL = CallMethodS (NameE (CURRENT id) _) "apply" targIL'* argIL'*

;;
;; Transition statement
;;

rule Stmt_ok/transs:
  p C f |- TransS expr : C f (TransS exprIL)
  -- Expr_ok: p C |- expr : exprIL
  -- if `(StateT; _) = $annot(exprIL)

;;
;; Declaration statement
;;

rule Stmt_ok/decls:
  p C f |- DeclS decl : C' f (DeclS declIL)
  -- Decl_ok: p C |- decl : C' declIL

;;
;; Statement sequence
;;

rule Stmts_ok/nil:
  p C f |- eps : C f eps

rule Stmts_ok/cons:
  p C f |- stmt_h :: stmt_t* : C'' f'' (stmtIL_h :: stmtIL_t*)
  -- Stmt_ok: p C f |- stmt_h : C' f' stmtIL_h
  -- Stmts_ok: p C' f' |- stmt_t* : C'' f'' stmtIL_t*
